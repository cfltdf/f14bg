<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" mouseOver="onMouseOver(event)" mouseOut="onMouseOut(event)">
	<mx:Script>
		<![CDATA[
			import com.f14.F14bgGame.TTA.manager.TTAImageManager;
			import com.f14.F14bgGame.TTA.TTAUtil;
			import com.f14.F14bgGame.TTA.ui.simple.ImageString;
			
			protected var workers:Array = new Array();
			protected var _num:int;
			public var selectable:Boolean;
			
			public function get num():int{
				return this._num;
			}
			
			public function set num(num:int):void{
				this._num = num;
				//设置工人的 显示情况
				for(var i:int=0;i<this.workers.length;i++){
					var worker:ImageString = this.workers[i];
					worker.visible = (i<num);
				}
			}
			
			/**
			 * 初始化
			 */
			public function init():void{
				//总共创建20个工人位置
				for(var i:int=0;i<20;i++){
					var worker:ImageString = new ImageString();
					if(i%2==0){
						this.hbox_2.addChild(worker);
					}else{
						this.hbox_1.addChild(worker);
					}
					worker.image.source = TTAImageManager.YELLOW_IMAGE;
					this.workers[i] = worker;
				}
				this.clear();
				this.addEventListener(MouseEvent.CLICK, onClick);
			}
			
			public function clear():void{
				this.num = 0;
			}
			
			/**
			 * 鼠标移到图像上时触发的事件
			 */
			protected function onMouseOver(evt:MouseEvent):void{
				if(selectable){
					//如果可选,则高亮显示该对象
					TTAUtil.effectManager.glowEffect.end();
					TTAUtil.effectManager.glowEffect.target = this;
					TTAUtil.effectManager.glowEffect.play();
				}
			}
			
			/**
			 * 鼠标移出图像时触发的事件
			 */
			protected function onMouseOut(evt:MouseEvent):void{
				//取消图像的高亮效果
				TTAUtil.effectManager.glowEffect.end();
			}
			
			/**
			 * 鼠标点击时触发的事件
			 */
			protected function onClick(evt:MouseEvent):void{
				if(this.selectable){
					//执行扩张人口
					TTAUtil.actionManager.increasePopulation();
				}
			}
			
		]]>
	</mx:Script>
	<mx:HBox id="hbox_1" height="26" horizontalGap="10">
	</mx:HBox>
	<mx:HBox id="hbox_2" y="38" height="26" horizontalGap="10">
	</mx:HBox>
</mx:Canvas>
