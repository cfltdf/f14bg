<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" height="40" horizontalScrollPolicy="auto" verticalScrollPolicy="off" xmlns:components="com.f14.F14bgGame.TS.components.*">
	<mx:Script>
		<![CDATA[
			import com.f14.F14bgGame.bg.ui.simple.SimpleCard;
			import com.f14.f14bg.components.ComponentOption;
			import com.f14.F14bgGame.TS.ui.simple.TSCardLabel;
			import com.f14.F14bgGame.TS.TSUtil;
			import com.f14.F14bgGame.TS.components.TSCard;
			protected var option:ComponentOption;
			protected var _cards:Array = new Array();
			protected var _selectable:Boolean;
			protected var _multiSelection:Boolean;
			
			public function init():void{
				this.option = new ComponentOption();
			}
			
			public function clear():void{
				this.option.removeAllComponent();
				//移除所有卡牌的监听器
				for each(var card:TSCardLabel in this._cards){
					if(card!=null){
						card.removeEventListener(MouseEvent.CLICK, onClick);
					}
				}
				this._cards = new Array();
				this.canvas_cards.removeAllChildren();
			}
			
			/**
			 * 设置是否可选
			 */
			public function set selectable(selectable:Boolean):void{
				this._selectable = selectable;
				var card:TSCardLabel;
				for each(card in this.canvas_cards.getChildren()){
					card.selectable = selectable;
				}
			}
			
			public function get selectable():Boolean{
				return this._selectable;
			}
			
			public function set multiSelection(multiSelection:Boolean):void{
				this.option.multiSelection = multiSelection;
			}
			
			/**
			 * 添加卡牌
			 */
			public function addCard(id:String):void{
				var card:TSCard = TSUtil.resourceManager.createTSCard(id);
				if(card!=null){
					var sc:TSCardLabel = new TSCardLabel();
					this.canvas_cards.addChild(sc);
					sc.init();
					sc.object = card;
					sc.selectable = this.selectable;
					this._cards[id] = sc;
					sc.addEventListener(MouseEvent.CLICK, onClick);
					//添加到选择组件中
					option.addComponent(sc);
				}
			}
			
			/**
			 * 添加卡牌
			 */
			public function addCards(cardIds:String):void{
				var ids:Array = cardIds.split(",");
				for each(var id:String in ids){
					this.addCard(id);
				}
			}
			
			/**
			 * 按照id取得卡牌
			 */
			public function getCard(id:String):TSCardLabel{
				return this._cards[id] as TSCardLabel;
			}
			
			/**
			 * 移除卡牌
			 */
			public function removeCard(id:String):void{
				var card:TSCardLabel = this.getCard(id);
				if(card!=null){
					this._cards[card.id] = null;
					card.removeEventListener(MouseEvent.CLICK, onClick);
					card.parent.removeChild(card);
					//从选择组件中移除
					this.option.removeComponent(card);
				}
			}
			
			/**
			 * 移除卡牌
			 */
			public function removeCards(cardIds:String):void{
				var ids:Array = cardIds.split(",");
				for each(var id:String in ids){
					this.removeCard(id);
				}
			}
			
			/**
			 * 点击卡牌时触发的方法
			 */
			protected function onClick(evt:MouseEvent):void{
				var card:TSCardLabel = evt.currentTarget as TSCardLabel;
				if(card.selectable){
					var param:Object;
					/*if(TSUtil.stateManager.currentConfirmWindow!=null){
						//如果存在询问窗口,则由该窗口返回指令参数
						param = {};
						param.cardId = card.id;
						TSUtil.stateManager.currentConfirmWindow.doConfirm(param);
					}*/
				}
			}
			
			/**
			 * 取得选中的卡牌
			 */
			public function getSelectedCard():SimpleCard{
				return this.option.selectedComponent as SimpleCard;
			}
			
			/**
			 * 设置选中的卡牌
			 */
			public function setSelectedCard(cardId:String):void{
				var card:TSCardLabel = this.getCard(cardId);
				if(card!=null){
					this.option.selectedComponent = card;
				}
			}
			
			/**
			 * 取得选中的卡牌
			 */
			public function getSelection():Array{
				return this.option.getSelection();
			}
			
		]]>
	</mx:Script>
	
	<components:CardCanvas id="canvas_cards" width="100%" height="100%" gap="4" />
</mx:Canvas>
