<?xml version="1.0" encoding="utf-8"?>
<InnoConfirmWindow xmlns="com.f14.F14bgGame.Innovation.ui.window.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="600" height="270">
	<mx:Script>
		<![CDATA[
			import com.f14.F14bgGame.bg.ui.simple.SimpleCard;
			import com.f14.core.util.ApplicationUtil;
			import mx.controls.Button;
			import mx.containers.HBox;
			import com.f14.F14bgGame.bg.utils.BgUtils;
			import com.f14.F14bgGame.Innovation.ui.simple.InnoCardLabel;
			import com.f14.F14bgGame.Innovation.ui.part.InnoPlayerHand;
			import mx.controls.HRule;
			
			private var cardsPart:InnoPlayerHand;
			
			/**
			 * 初始化相关组件
			 */
			override public function initComponents():void{
				super.initComponents();
				//创建分割线...
				var rule:HRule = new HRule;
				rule.width = this.width*0.8;
				rule.x = (this.width-rule.width)/2;
				rule.y = 80;
				this.addChild(rule);
				
				cardsPart = new InnoPlayerHand();
				cardsPart.width = this.width*0.8;
				cardsPart.x = (this.width-cardsPart.width)/2;
				cardsPart.y = 90;
				this.addChild(cardsPart);
				cardsPart.init();
				//设置为单选
				cardsPart.selectable = true;
				cardsPart.setEventTrigOnSelected(false);
				
				var hbox:HBox = new HBox();
				hbox.width = 150;
				hbox.x = (this.width-hbox.width)/2;
				hbox.y = 160;
				this.addChild(hbox);
				var button:Button = new Button();
				button.label = "<-";
				button.width = 60;
				button.addEventListener(MouseEvent.CLICK, ApplicationUtil.createClickFunction(moveCard, -1));
				hbox.addChild(button);
				
				button = new Button();
				button.label = "->";
				button.width = 60;
				button.addEventListener(MouseEvent.CLICK, ApplicationUtil.createClickFunction(moveCard, 1));
				hbox.addChild(button);
				
			}
			
			/**
			 * 按照参数装载窗口内容
			 */
			override public function loadParam(param:Object):void{
				super.loadParam(param);
				this.loadCards(param.sortCardIds);
			}
			
			/**
			 * 清除窗口内容
			 */
			override public function clear():void{
				super.clear();
				if(this.cardsPart!=null){
					this.cardsPart.clear();
				}
			}
			
			/**
			 * 装载卡牌信息
			 */
			protected function loadCards(cardIds:String):void{
				this.cardsPart.addCards(cardIds);
			}
			
			/**
			 * 创建确认时的参数
			 */
			override protected function createConfirmParam():Object{
				//取得cardsPart中的所有牌,作为参数传入
				var cards:Array = this.cardsPart.getSortedCards();
				var param:Object = {};
				param.cardIds = BgUtils.label2StringEach(cards);
				return param;
			}
			
			/**
			 * 移除待选卡牌
			 */
			public function removeCards(cardIds:String):void{
				this.cardsPart.removeCards(cardIds);
			}
			
			/**
			 * 移动卡牌
			 */
			public function moveCard(e:Event, offset:int):void{
				var card:SimpleCard = this.cardsPart.getSelectedCard();
				if(!card){
					ApplicationUtil.alert("请选择要调整位置的牌!");
				}else{
					this.cardsPart.moveCard(card, offset);
				}
			}
			
		]]>
	</mx:Script>
</InnoConfirmWindow>
