<?xml version="1.0" encoding="utf-8"?>
<module:F14GameModule xmlns:module="com.f14.F14bgGame.bg.ui.*" xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="100%" height="100%" creationComplete="init()" xmlns:ui="com.f14.F14bgGame.Tichu.ui.*" resize="onResize()">
	<mx:Script>
		<![CDATA[
			import com.f14.F14bgGame.Tichu.ui.part.TichuSetupBoard;
			import com.f14.F14bgGame.Tichu.TichuUtil;
			import com.f14.F14bgGame.Tichu.player.TichuPlayerHandler;
			import com.f14.F14bgGame.Tichu.player.TichuPlayer;
			import com.f14.core.util.Version;
			import com.f14.F14bgGame.bg.handler.InGameHandler;
			import com.f14.F14bgGame.bg.player.Player;
			import com.f14.F14bgClient.room.ui.part.SetupBoard;
			import com.f14.F14bgClient.room.RoomUtil;
			
			/**
			 * 创建version对象
			 */
			override protected function createVersion():Version{
				var v:Version = super.createVersion();
				v.descr = "素材取自BSW,感谢它.\n"
					+ "感谢所有朋友的测试和支持.";
				return v;
			}
		
			override public function init():void{
				this.loadVersionInfo();
				super.init();
				this.mainBoard = this.tichuMainBoard;
				
				RoomUtil.gameModule = this;
				TichuUtil.initGameModule(this);
				
				//初始化图片资源管理器
				this.createLoadingMask();
				TichuUtil.initImageManager();
				//检查图片管理器是否执行完成
				TichuUtil.imageManager.checkImageLoadComplete();
			}
			
			/**
			 * 图片资源装载完成时的回调函数
			 */
			override public function onImageLoadComplete():void{
				super.onImageLoadComplete();
				TichuUtil.init();
				
				//装载资源字符串
				TichuUtil.resourceManager.loadResourceString();
				this.mainBoard.init();
				
				//装载房间信息
				RoomUtil.actionManager.loadRoomInfo();
			}
			
			override protected function createGameCommandHandler():InGameHandler{
				return new TichuPlayerHandler();
			}
			
			override protected function createSetupBoard():SetupBoard{
				return new TichuSetupBoard();
			}
			
			override public function createPlayer():Player{
				return new TichuPlayer();
			}
			
			public function onResize():void{
				//变换大小时,同时调整游戏面板的大小
				var scale:Number = this.height / TichuMainBoard.BOARD_HEIGHT;
				if(scale>1){
					scale = 1;
				}
				this.tichuMainBoard.scaleX = scale;
				this.tichuMainBoard.scaleY = scale;
			}
			
		]]>
	</mx:Script>
	<ui:TichuMainBoard id="tichuMainBoard" />
</module:F14GameModule>
